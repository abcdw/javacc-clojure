options {
  LOOKAHEAD = 2;
  UNICODE_INPUT = true;
}

PARSER_BEGIN(ClojureParser)

public class ClojureParser {
    static int forms = 0;
    static int literals = 0;
    static int keywords = 0;
    static int symbols = 0;
    static int lists = 0;
    static int vectors = 0;
    static int maps = 0;
    static int sets = 0;
    static int nestiness = 0;
    static int mxnestiness = 0;
    static int functions = 0;
    static int pfunctions = 0;
    static int macros = 0;
    static int multis = 0;
    static int methods = 0;

    public static void main(String[] args) {
        try {
            new ClojureParser(new java.io.InputStreamReader(System.in)).S();
            System.out.println("Syntax is okay");
            System.out.println(
                               "|===============Forms==================\n" +
                               "|forms: " + forms + "\n" +
                               "|symbols: " + symbols + "\n" +
                               "|literals: " + literals + "\n" +
                               "|keywords: " + keywords + "\n" +
                               "|maximum form nestiness: " + mxnestiness + "\n" +
                               "|===============Collections============\n" +
                               "|lists: " + lists + "\n" +
                               "|vectors: " + vectors + "\n" +
                               "|maps: " + maps + "\n" +
                               "|sets: " + sets + "\n" +
                               "|===============Definitions============\n" +
                               "|public function defenitions: " + functions + "\n" +
                               "|private function defenitions: " + pfunctions + "\n" +
                               "|macro definitions: " + macros + "\n" +
                               "|multimethods defs: " + multis + "\n" +
                               "|multimethods implementations: " + methods + "\n" +
                               ""
                               );
        } catch (Throwable e) {
            System.out.println("Syntax check failed: " + e.getMessage());
        }
    }
}

PARSER_END(ClojureParser)



/* COMMENTS */

SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: ";" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

SKIP:  { " " | "\t" | "\n" | "\r" | ","              }

/* BRACKETS */

TOKEN: {
  // <BRACKET: <LPAR> | <RPAR> | <LSQU> | <RSQU> | <LCUR> | <RCUR> >
  <LPAR: "(">
  | <RPAR: ")">
  | <LSQU: "[">
  | <RSQU: "]">
  | <LCUR: "{">
  | <RCUR: "}">
  }


/* LITERALS */
TOKEN: {
  <NUM: <RATIO> | <INT> | <DOUBLE> >
  | <RATIO: <INT> "/" <INT> >
  | <DOUBLE: (<DIGIT>)+ "." (<DIGIT>)* ("e" (<DIGIT>)+)? >
  | <INT: (<DIGIT>)+ ("r" (<DIGIT>)+)? ("n" | "l" | "L" | "N")?>
  | <NIL: "nil">
  | <STR: "\"" (~["\""] | "\\" "\"")* "\"">
  | <BOOLEAN: "true" | "false">
  | <CHARACTER: "\\" (~[" "]
                     | ("u" <DIGIT><DIGIT><DIGIT><DIGIT>)
                     | ("o" <DIGIT><DIGIT><DIGIT>)
                     | "newline"
                     | "space"
                     | "tab"
                     | "formfeed"
                     | "backspace"
                     | "return") >
  | <KEYWORD: ":" (":")? ((<DIGIT>)* <NS> "/")? (<DIGIT>)* <NAME> >
}

TOKEN: {
  <SYMBOL: <NAME> | <NS> | <NS> "/" <NAME> | <INTEROP> >
  | <INTEROP: (("." | ".-") <NAME> | "." )>
  | <NS: <NAME> ("." <NAME>)*>
  | <NAME: (<LETTER> | <CHAR>) (<DIGIT> | <LETTER> | <CHAR> | "#" | "'" )* >
  | <LETTER: ["a"-"z", "A"-"Z"]>
  | <CHAR: ["*", "+", "!", "-", "_", "?", "=", "%", "&", ">", "<", "$"] > // maybe some other chars
  | <DIGIT: ["0"-"9"]>
}

void S(): {} { forms() <EOF> }

void forms(): {} {
  (form() {
    ++forms;
    if (mxnestiness < nestiness)
      mxnestiness = nestiness;
  })*
}
void form(): {} {
  literal() { literals++; }
  | symbol()
  | list()
  | vector()
  | set()
  | map()
  | reader_macro()
}

void list(): {++nestiness;} {
  <LPAR> forms() <RPAR> {
    ++lists;
    --nestiness;
  }
}

void vector(): {} { <LSQU> forms() <RSQU> { ++vectors; } }
void set(): {} { "#" <LCUR> forms() <RCUR> { ++sets; } }
void map(): {} { <LCUR> (form() form())* <RCUR> { ++maps; } }

void symbol(): {Token t;} {
  t=<SYMBOL> {
    ++symbols;
    switch (t.image) {
      case "defmulti": ++multis; break;
      case "defmethod": ++methods; break;
      case "defn": ++functions; break;
      case "defn-": ++pfunctions; break;
      case "defmacro": ++macros; break;
      default: break;
    }
  }
}
void literal(): {} { <KEYWORD> { ++keywords; } | <CHARACTER> | <NUM> | <NIL> | <STR> | <BOOLEAN> }


/* READER MACROS */

void reader_macro(): {} { quote() | lambda() | regex() | var_quote() | discard() | tag() | meta()}
void quote(): {} { ("\'" | "`" | "~" | "~@" | "@" ) form() }
void lambda(): {} { "#(" forms() ")" }
void regex(): {} { "#" <STR> }
void var_quote(): {} { "#\'" symbol() }
void discard(): {} { "#_" form() }
void tag(): {} { "^" form() form() }
void meta(): {} {"#^" (map() form() | form()) }


// void deref(): {} { "@" form() }
// void dispatch(): {} { "#" symbol() form() }
